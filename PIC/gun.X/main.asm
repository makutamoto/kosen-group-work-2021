	LIST	P=PIC16F648A
	INCLUDE	<P16F648A.INC>
	__CONFIG 	_CP_OFF & _MCLRE_OFF & _PWRTE_OFF & _WDT_OFF & _HS_OSC & _LVP_OFF & _BOREN_OFF    

; Constants
CAMERA_ADDRESS	EQU		H'58'
	
; Pin definitions
I2C_SCL		EQU		H'03'
I2C_SDA		EQU		H'04'
	
; ALL BANK
ARG0		EQU		H'70'
ARG1		EQU		H'71'
ARG2		EQU		H'72'
TEMP0		EQU		H'73'
TEMP1		EQU		H'74'

		ORG		H'0000'
INIT		
		MOVLW		H'07'
		MOVWF		CMCON
		BSF		STATUS,RP0	;Set Bank 1
		CLRF		TRISA		;Set RA7-5 to Output and RA4-0 to Input 
		MOVLW		B'00000110'
		MOVWF		TRISB		;Set Port-B to all Output
		MOVLW		D'131'
		MOVWF		PR2
		
		MOVLW		B'11010010'
		MOVWF		OPTION_REG
		
		BCF		STATUS,RP0	;Set Bank 0
		MOVLW		B'00000001'
		MOVWF		PORTA
		CLRF		PORTB
		
		MOVLW		B'00010001'
		MOVWF		T1CON
		
		MOVLW		B'00110000'
		IORWF		CCP1CON
		MOVLW		B'00111111'
		MOVWF		CCPR1L
		BSF		T2CON, TMR2ON
		
		;MOVLW		B'11000000'
		;IORWF		INTCON, F
		
		BSF		RCSTA, SPEN
		BSF		STATUS, RP0
		BCF		TXSTA, SYNC
		BSF		TXSTA, BRGH
		MOVLW		D'10'
		MOVWF		SPBRG
		BSF		TXSTA, TXEN
		BCF		STATUS, RP0
		
		CALL		INIT_CAMERA
		
		GOTO		START

UART
		MOVWF		TXREG
		BSF		STATUS, RP0
		BTFSS		TXSTA, TRMT
		GOTO		$ - 1
		BCF		STATUS, RP0
		RETURN
		
I2C_SCL_LOW
		BCF		PORTA, I2C_SCL
		CALL		NOP10CYCLES
		RETURN

I2C_SCL_HIGH
		BSF		PORTA, I2C_SCL
		CALL		NOP10CYCLES
		RETURN
		
I2C_SCL_INPUT
		BSF		STATUS, RP0
		BSF		TRISA, I2C_SCL
		BCF		STATUS, RP0
		RETURN
		
I2C_SCL_OUTPUT
		BSF		STATUS, RP0
		BCF		TRISA, I2C_SCL
		BCF		STATUS, RP0
		RETURN
		
I2C_SDA_LOW
		BCF		PORTA, I2C_SDA
		CALL		NOP10CYCLES
		RETURN

I2C_SDA_HIGH
		BSF		PORTA, I2C_SDA
		CALL		NOP10CYCLES
		RETURN
		
I2C_SDA_INPUT
		BSF		STATUS, RP0
		BSF		TRISA, I2C_SDA
		BCF		STATUS, RP0
		RETURN
		
I2C_SDA_OUTPUT
		BSF		STATUS, RP0
		BCF		TRISA, I2C_SDA
		BCF		STATUS, RP0
		RETURN
		
I2C_START_CONDITION
		CALL		I2C_SDA_LOW
		CALL		I2C_SCL_LOW
		RETURN
		
I2C_STOP_CONDITION
		CALL		I2C_SCL_HIGH
		CALL		I2C_SDA_HIGH
		RETURN
		
I2C_SEND_DATA
		MOVWF		TEMP0
		MOVLW		H'08'
		MOVWF		TEMP1
I2C_SEND_DATA_LOOP
		BTFSC		TEMP0, 7
		GOTO		I2C_SEND_DATA_LOOP_SDA_1
		CALL		I2C_SDA_LOW
		GOTO		I2C_SEND_DATA_LOOP_SDA_END
I2C_SEND_DATA_LOOP_SDA_1
		CALL		I2C_SDA_HIGH
I2C_SEND_DATA_LOOP_SDA_END
		RLF		TEMP0, F
		CALL		I2C_SCL_HIGH
		CALL		I2C_SCL_LOW
		DECFSZ		TEMP1, F
		GOTO		I2C_SEND_DATA_LOOP
		CALL		I2C_SDA_HIGH
		CALL		I2C_SDA_INPUT
		CALL		I2C_SCL_HIGH
		BTFSC		PORTA, I2C_SDA
		GOTO		I2C_SEND_DATA_SUCCESS
		CALL		I2C_SCL_LOW
		CALL		I2C_SDA_OUTPUT
		CALL		I2C_SDA_LOW
		RETLW		H'01'
I2C_SEND_DATA_SUCCESS
		CALL		I2C_SCL_LOW
		CALL		I2C_SDA_OUTPUT
		CALL		I2C_SDA_LOW
		CALL		I2C_SCL_INPUT
		BTFSS		PORTA, I2C_SCL
		GOTO		$ - 1
		CALL		I2C_SCL_OUTPUT
		CALL		I2C_SCL_LOW
		RETLW		H'00'
		
I2C_READ_DATA
		CLRF		TEMP0
		CALL		I2C_SDA_INPUT
		MOVLW		H'08'
		MOVWF		TEMP1
I2C_READ_DATA_LOOP
		RLF		TEMP0, F
		CALL		I2C_SCL_HIGH
		BTFSC		PORTA, I2C_SDA
		BSF		TEMP0, 0
		CALL		I2C_SCL_LOW
		DECFSZ		TEMP1, F
		GOTO		I2C_READ_DATA_LOOP
		CALL		I2C_SDA_OUTPUT
		CALL		I2C_SDA_LOW
		CALL		I2C_SCL_HIGH
		CALL		I2C_SCL_LOW
		MOVFW		TEMP0
		CALL		UART
		RETURN
	
WRITE_CAMERA	; ARG0: Address, ARG1: data
		CALL		I2C_START_CONDITION
		MOVLW		CAMERA_ADDRESS << 1
		CALL		I2C_SEND_DATA
		ADDLW		H'00'
		BTFSC		STATUS, Z
		GOTO		WRITE_CAMERA_FAIL
		MOVFW		ARG0
		CALL		I2C_SEND_DATA
		ADDLW		H'00'
		BTFSC		STATUS, Z
		GOTO		WRITE_CAMERA_FAIL
		MOVFW		ARG1
		CALL		I2C_SEND_DATA
		ADDLW		H'00'
		BTFSC		STATUS, Z
		GOTO		WRITE_CAMERA_FAIL
		CALL		I2C_STOP_CONDITION		
		RETURN
WRITE_CAMERA_FAIL
		CALL		I2C_STOP_CONDITION
		GOTO		WRITE_CAMERA		

READ_CAMERA
		CALL		I2C_START_CONDITION
		MOVLW		CAMERA_ADDRESS << 1
		CALL		I2C_SEND_DATA
		ADDLW		H'00'
		BTFSC		STATUS, Z
		GOTO		READ_CAMERA_FAIL
		MOVLW		H'36'
		CALL		I2C_SEND_DATA
		ADDLW		H'00'
		BTFSC		STATUS, Z
		GOTO		READ_CAMERA_FAIL
		CALL		I2C_STOP_CONDITION
		
		CALL		I2C_START_CONDITION
		MOVLW		CAMERA_ADDRESS << 1 | 1
		CALL		I2C_SEND_DATA
		ADDLW		H'00'
		BTFSC		STATUS, Z
		GOTO		READ_CAMERA_FAIL
		CALL		I2C_READ_DATA
		CALL		I2C_READ_DATA
		CALL		I2C_READ_DATA
		CALL		I2C_READ_DATA
		CALL		I2C_READ_DATA
		CALL		I2C_READ_DATA
		CALL		I2C_READ_DATA
		CALL		I2C_READ_DATA
		CALL		I2C_READ_DATA
		CALL		I2C_READ_DATA
		CALL		I2C_READ_DATA
		CALL		I2C_READ_DATA
		CALL		I2C_READ_DATA
		CALL		I2C_READ_DATA
		CALL		I2C_READ_DATA
		CALL		I2C_READ_DATA
		CALL		I2C_STOP_CONDITION		
		RETURN
READ_CAMERA_FAIL
		CALL		I2C_STOP_CONDITION
		GOTO		READ_CAMERA		
		
INIT_CAMERA
		MOVLW		H'30'
		MOVWF		ARG0
		MOVLW		H'01'
		MOVWF		ARG1
		CALL		WRITE_CAMERA
		
		MOVLW		H'30'
		MOVWF		ARG0
		MOVLW		H'08'
		MOVWF		ARG1
		CALL		WRITE_CAMERA
		
		MOVLW		H'06'
		MOVWF		ARG0
		MOVLW		H'90'
		MOVWF		ARG1
		CALL		WRITE_CAMERA
		
		MOVLW		H'08'
		MOVWF		ARG0
		MOVLW		H'C0'
		MOVWF		ARG1
		CALL		WRITE_CAMERA
		
		MOVLW		H'1A'
		MOVWF		ARG0
		MOVLW		H'40'
		MOVWF		ARG1
		CALL		WRITE_CAMERA
		
		MOVLW		H'33'
		MOVWF		ARG0
		MOVLW		H'33'
		MOVWF		ARG1
		CALL		WRITE_CAMERA
		
		RETURN

IR_WAIT_1BIT
		CLRF		TMR0
		BCF		INTCON, T0IF
		BTFSS		INTCON, T0IF
		GOTO		$ - 1
		RETURN
		
IR_SEND_0
		MOVLW		B'00001100'
		IORWF		CCP1CON, F
		CALL		IR_WAIT_1BIT
		RETURN
		
IR_SEND_1
		MOVLW		B'11110011'
		ANDWF		CCP1CON, F
		CALL		IR_WAIT_1BIT
		RETURN
		
IR_SEND_BYTE
		MOVWF		TEMP0
		MOVLW		H'08'
		MOVWF		TEMP1
IR_SEND_BYTE_LOOP
		BTFSC		TEMP0, 7
		GOTO		IR_SEND_BYTE_LOOP_DATA_1
		CALL		IR_SEND_0
		GOTO		IR_SEND_BYTE_LOOP_DATA_END
IR_SEND_BYTE_LOOP_DATA_1
		CALL		IR_SEND_1
IR_SEND_BYTE_LOOP_DATA_END
		RLF		TEMP0, F
		DECFSZ		TEMP1, F
		GOTO		IR_SEND_BYTE_LOOP
		RETURN
		
IR_COOLDOWN
		CLRF		TMR1H
		CLRF		TMR1L
		BCF		PIR1, TMR1IF
		BTFSS		PIR1, TMR1IF
		GOTO		$ - 1
		RETURN
		
IR_SEND_DATA	; ARG0: data 0, ARG1: data 1, ARG2: data 2
		CALL		IR_SEND_0
		MOVFW		ARG0
		CALL		IR_SEND_BYTE
		MOVFW		ARG1
		CALL		IR_SEND_BYTE
		MOVFW		ARG2
		CALL		IR_SEND_BYTE
		CALL		IR_SEND_1
		CALL		IR_COOLDOWN
		RETURN
		
START	
		;CALL		READ_CAMERA
		
		MOVLW		H'05'
		MOVWF		ARG0
		MOVLW		H'05'
		MOVWF		ARG1
		MOVLW		H'02'
		MOVWF		ARG2
		CALL		IR_SEND_DATA
		
		GOTO		START
		
;Subroutine for waiting 10-micro-second		;0.25-micro*4-Clock*(2+8)-Cycle=10-micro
NOP10CYCLES	NOP				;1-Cycle
		NOP				;1-Cycle
		NOP				;1-Cycle
		NOP				;1-Cycle
		NOP				;1-Cycle
		NOP				;1-Cycle
		RETURN				;2-Cycle  Total 8-Cycle
		
		END
