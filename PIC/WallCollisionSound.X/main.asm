	LIST	P=PIC16F648A
	INCLUDE	<P16F648A.INC>
	__CONFIG 	_CP_OFF & _MCLRE_OFF & _PWRTE_ON & _WDT_OFF & _EXTCLK_OSC & _LVP_OFF & _BOREN_OFF    
; bank 0
STATUS_TEMP	EQU		H'20'
	
; ALL BANK
ARG0		EQU		H'70'
ARG1		EQU		H'71'
TEMP0		EQU		H'72'
TEMP1		EQU		H'73'
SLAVE_FREE	EQU		H'74'
W_TEMP		EQU		H'75'
INT_TEMP0	EQU		H'76'
INT_TEMP1	EQU		H'77'
SOUND_FLAG	EQU		H'78'
DATAINDEXL	EQU		H'79'
DATAINDEXH	EQU		H'7A'
PCLATH_TEMP	EQU		H'7B'
DATAINDEX_POSTSCALER	EQU	H'7C'
	
;Constannt Definition
MUSICTEMPO	EQU		H'1588'		;Music Tempo:120-sibuonpu/s->H"1588"=D"5000"

		ORG		H'00'
		GOTO		INIT
		ORG		H'04'
INTERRUPT
		MOVWF		W_TEMP
		SWAPF		STATUS, W
		BCF		STATUS, RP0
		MOVWF		STATUS_TEMP
		
		MOVFW		PCL
		MOVFW		PCLATH
		MOVWF		PCLATH_TEMP
		
		BTFSC		INTCON, INTF
		GOTO		INT_MATCH
		BTFSC		PIR1, TMR2IF
		GOTO		TMR2_MATCH
		GOTO		INTERRUPT_END
INT_MATCH	
		BSF		SOUND_FLAG, 0
		
		BCF		INTCON, INTF
		GOTO		INTERRUPT_END
TMR2_MATCH
		INCF		DATAINDEX_POSTSCALER, F
		MOVFW		DATAINDEX_POSTSCALER
		SUBLW		H'09'
		BTFSS		STATUS, Z
		GOTO		TMR2_MATCH_END
		CLRF		DATAINDEX_POSTSCALER
		
		BCF		CCP1CON, CCP1Y
		BCF		CCP1CON, CCP1X
		MOVLW		high SOUND_DATA
		MOVWF		PCLATH
		CALL		SOUND_DATA
		MOVWF		INT_TEMP0
		MOVLW		high TMR2_MATCH
		MOVWF		PCLATH
		MOVFW		INT_TEMP0
		BTFSC		STATUS, Z
		GOTO		SOUND_END
		MOVWF		INT_TEMP0
		BTFSC		INT_TEMP0, 0
		BSF		CCP1CON, CCP1Y
		BTFSC		INT_TEMP0, 1
		BSF		CCP1CON, CCP1X
		RRF		INT_TEMP0, F
		RRF		INT_TEMP0, W
		ANDLW		H'3F'
		MOVWF		CCPR1L
		INCF		DATAINDEXL, F
		BTFSC		STATUS, Z
		INCF		DATAINDEXH, F
		GOTO		TMR2_MATCH_END
SOUND_END
		BSF		STATUS, RP0
		BCF		PIE1, TMR2IE
		BCF		STATUS, RP0
TMR2_MATCH_END
		BCF		PIR1, TMR2IF
		GOTO		INTERRUPT_END
		
INTERRUPT_END
		MOVFW		PCLATH_TEMP
		MOVWF		PCLATH
		
		SWAPF		STATUS_TEMP, W
		MOVWF		STATUS
		SWAPF		W_TEMP, F
		SWAPF		W_TEMP, W
		
		RETFIE
INIT		
		MOVLW		H'07'
		MOVWF		CMCON
		CLRF		PORTA
		CLRF		PORTB
		
		BSF		STATUS,RP0
		CLRF		TRISA
		MOVLW		B'00000001'
		MOVWF		TRISB
		MOVLW		H'3F'
		MOVWF		PR2
		
		MOVLW		B'00000010'
		MOVWF		PIE1
		
		BCF		STATUS, RP0
		
		MOVLW		B'00001100'
		MOVWF		CCP1CON
		MOVLW		H'0F'
		MOVWF		CCPR1L
		MOVLW		B'00000100' ; TMR2 ON
		MOVWF		T2CON
		
		MOVLW		B'11010000'
		MOVWF		INTCON
		
		MOVLW		SOUND_LENGTH_LOW
		MOVWF		DATAINDEXL
		MOVLW		SOUND_LENGTH_HIGH
		MOVWF		DATAINDEXH
		
		CLRF		SOUND_FLAG
		
		GOTO		MAIN

MAIN
		BTFSS		SOUND_FLAG, 0
		GOTO		MAIN
		CALL		SOUND
		CLRF		SOUND_FLAG
		GOTO		MAIN
		

SOUND
		BCF		CCP1CON, CCP1X
		BCF		CCP1CON, CCP1Y
		MOVLW		B'00100000'
		MOVWF		CCPR1L
		CLRF		DATAINDEXL
		CLRF		DATAINDEXH
		CLRF		DATAINDEX_POSTSCALER
		
		BSF		STATUS, RP0
		BSF		PIE1, TMR2IE
		BCF		STATUS, RP0
		
		RETURN

		ORG		H'0200'
SOUND_DATA
		MOVFW		DATAINDEXH
		ADDWF		PCLATH, W
		MOVWF		INT_TEMP0
		MOVFW		DATAINDEXL
		ADDLW		H'07'
		BTFSC		STATUS, C
		INCF		INT_TEMP0, F
		ADDWF		PCL, W
		BTFSC		STATUS, C
		INCF		INT_TEMP0, F
		MOVWF		INT_TEMP1
		MOVFW		INT_TEMP0
		MOVWF		PCLATH
		MOVFW		INT_TEMP1
		MOVWF		PCL
		INCLUDE	"SOUND.INC"
		
		END
