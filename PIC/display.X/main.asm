	LIST	P=PIC16F648A
	INCLUDE	<P16F648A.INC>
	__CONFIG 	_CP_OFF & _MCLRE_OFF & _PWRTE_OFF & _WDT_OFF & _HS_OSC & _LVP_OFF & _BOREN_OFF    

; PIN definition
VSYNC_PIN	EQU		H'00'
HSYNC_PIN	EQU		H'02'
	
; Bank 0
SCREEN_BUFFER0	EQU		H'20'	; 0x20-3D
SCREEN_BUFFER1	EQU		H'3E'	; 0x3E-5B
; Sprite area, each sprite occuoy 3 bytes: Y X TILE
SPRITE0		EQU		H'5C'
SPRITE1		EQU		H'5F'
SPRITE2		EQU		H'62'
SPRITE3		EQU		H'65'
SPRITE4		EQU		H'68'
SPRITE5		EQU		H'6B'
SPRITE6		EQU		H'6E'

; Bank 1
TILE_MAP	EQU		H'A0'	; 0xA0-EF
	
; ALL BANK
TEMP0		EQU		H'74'
TEMP1		EQU		H'75'
TILE_ROW	EQU		H'76'
MAP_INDEX	EQU		H'77'
MAP_INDEX_TEMP	EQU		H'78'
FSR_BACK	EQU		H'79'
FILL_COLOR	EQU		H'7A' ; B'BBBB000A' A: Flag, B: Color
CURRENT_BUFFER	EQU		H'7B'
DRAWED_PAST	EQU		H'7C'
TILE_ROW_BACK	EQU		H'7D'
TEMP2		EQU		H'7E'
CURRENT_BUFFER_ADDR	EQU	H'7F'

COLORBAR	MACRO
	VARIABLE I
I = 0
	WHILE	I < D'45'
		MOVLW	((I & H'0F') << 4) | ((I + 1) & H'0F')
		MOVWF	SCREEN_BUFFER + I / 2
I += 2
	ENDW
		ENDM
		
CLEAR_TILEMAP	MACRO
		LOCAL		LOOP
		MOVLW		TILE_MAP
		MOVWF		FSR
		CLRF		TEMP1
		MOVLW		D'80'
		MOVWF		TEMP0
LOOP:
		MOVFW		TEMP1
		MOVWF		INDF
		INCF		TEMP1, F
		ADDLW		-D'29'
		BTFSC		STATUS, Z
		CLRF		TEMP1
		INCF		FSR, F
		DECFSZ		TEMP0, F
		GOTO		LOOP
		ENDM
	
		ORG		H'0000'		;Reset Start
INIT		MOVLW		H'07'
		MOVWF		CMCON
		
		BSF		STATUS,RP0
		CLRF		TRISA 
		CLRF		TRISB
		
		BSF		RCSTA, SPEN
		BSF		STATUS, RP0
		BSF		TRISB, 1
		BSF		TRISB, 2
		BCF		TXSTA, SYNC
		MOVLW		D'207'
		MOVWF		SPBRG
		BSF		TXSTA, TXEN
		
		BCF		STATUS,RP0	;Set Bank 0
		BSF		PORTA, 0
		CLRF		PORTB
		
		CLRF		CURRENT_BUFFER
		
		CLEAR_TILEMAP
		
		CLRF		DRAWED_PAST
		MOVLW		H'01'
		MOVWF		FILL_COLOR
		
		MOVLW		-H'0A'
		MOVWF		SPRITE0
		MOVLW		D'5'
		MOVWF		SPRITE0 + 1
		MOVLW		D'30'
		MOVWF		SPRITE0 + 2
		
		CLRF		SPRITE1

CALC_PIXELS	MACRO
		MOVLW		high TILE_TABLE
		MOVWF		PCLATH
		MOVFW		FSR
		MOVWF		FSR_BACK
		MOVFW		MAP_INDEX_TEMP
		MOVWF		FSR
		MOVFW		INDF
		MOVWF		TEMP1
		INCF		MAP_INDEX_TEMP, F
		MOVFW		FSR_BACK
		MOVWF		FSR
		MOVFW		TEMP1
		CALL		TILE_TABLE
		ENDM
		
DRAW_BUFFER	MACRO	BUFFER
	VARIABLE I
		NOP
		NOP
I = 0
	WHILE	I < D'30'
		MOVFW		BUFFER + I
		MOVWF		PORTB
		SWAPF		BUFFER + I, W
		MOVWF		PORTB
I += 1
	ENDW
		NOP
		CLRF		PORTB
		ENDM
		
HSYNC_TYPE_VSYNCSTART	EQU	H'00'
HSYNC_TYPE_VSYNCEND	EQU	H'01'
HSYNC_TYPE_CALIB	EQU	H'02'
HSYNC_TYPE_PRE_DRAW	EQU	H'03' ; ARG1: PREDRAW_NUM
HSYNC_TYPE_DRAW		EQU	H'04'
HSYNC_TYPE_END		EQU	H'05'
HSYNC_TYPE_NOP		EQU	H'06'
HSYNC		MACRO		TYPE, ARG0	; Wait 18 instructions after SYNC.
	;   Start of rising edge
	LOCAL		FILL_DRAW, DRAW_BUFFER1, END_DRAW, SPRITE_UPPER, SPRITE_LOWER, SPRITE_END, NOT_ONE_LINE, ONE_LINE
	variable	I
	IF	TYPE == HSYNC_TYPE_VSYNCSTART
		MOVLW		B'0000101'
		XORWF		PORTA, F
	ELSE
		NOP	
		BSF		PORTA, HSYNC_PIN
	ENDIF
	;   End of falling edge
	
	;	free space 139 cycles
	IF	TYPE == HSYNC_TYPE_PRE_DRAW
		BSF		STATUS, RP0
		CALC_PIXELS
		CALC_PIXELS	
	IF	ARG0 < 2
		CALC_PIXELS
		CALC_PIXELS
		CLRF		PCLATH
		BCF		STATUS, RP0
	ELSE
		BCF		STATUS, RP0
		CLRF		PCLATH
		MOVLW		H'0A'
		ADDWF		TILE_ROW
		MOVLW		D'60'
		SUBWF		TILE_ROW, W
		BTFSS		STATUS, Z
		GOTO		NOT_ONE_LINE
		CLRF		TILE_ROW
		MOVLW		H'0A'
		ADDWF		MAP_INDEX, F
		MOVLW		H'F0'
		SUBWF		MAP_INDEX, W
		MOVLW		TILE_MAP
		BTFSC		STATUS, Z
		MOVWF		MAP_INDEX
		GOTO		ONE_LINE
NOT_ONE_LINE:
		CALL		NOP8CYCLES
		NOP
ONE_LINE:	
		MOVFW		TILE_ROW
		MOVWF		TILE_ROW_BACK
		MOVFW		SPRITE0
		MOVWF		TILE_ROW
		BTFSC		SPRITE0, 7
		GOTO		SPRITE_UPPER
		SUBLW		H'05'
		BTFSS		STATUS, C
		GOTO		SPRITE_LOWER
		BCF		STATUS, C
		RLF		TILE_ROW, F
		RLF		TILE_ROW, F
		RLF		TILE_ROW, F
		RLF		SPRITE0, W
		ADDWF		TILE_ROW, F
		MOVLW		high TILE_TABLE
		MOVWF		PCLATH
		MOVFW		SPRITE0 + 1
		ADDWF		CURRENT_BUFFER_ADDR, W
		MOVWF		FSR
		MOVFW		SPRITE0 + 2
		CALL		TILE_TABLE
		CLRF		PCLATH
		GOTO		SPRITE_END
SPRITE_UPPER:
		NOP
		NOP
		NOP
SPRITE_LOWER:
		CALL	    NOP10CYCLES
		CALL	    NOP10CYCLES
		CALL	    NOP8CYCLES
		CALL	    NOP8CYCLES
SPRITE_END:
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
	ENDIF
	ELSE
	IF	TYPE == HSYNC_TYPE_DRAW
		NOP
		NOP
		NOP
		NOP
		NOP
		BTFSC		FILL_COLOR, 0
		GOTO		FILL_DRAW
		BTFSC		CURRENT_BUFFER, 0
		GOTO		DRAW_BUFFER1
		NOP
		DRAW_BUFFER	SCREEN_BUFFER0
		NOP
		NOP
		GOTO		END_DRAW
DRAW_BUFFER1:
		DRAW_BUFFER	SCREEN_BUFFER1
		NOP
		NOP
		GOTO		END_DRAW
FILL_DRAW:
		NOP
		NOP
		NOP
		NOP
		MOVFW		FILL_COLOR
		MOVWF		PORTB
I = 0
	WHILE	I < D'11'
		CALL		NOP10CYCLES
I += 1
	ENDW
		CALL		NOP8CYCLES
		NOP
		CLRF		PORTB
		NOP
		NOP
		NOP
		NOP
		
END_DRAW:	
		NOP
	ELSE
	WHILE	I < D'13'
		CALL		NOP10CYCLES
I += 1
	ENDW
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
	ENDIF
	ENDIF
	
	;   Start of falling edge
	IF	TYPE == HSYNC_TYPE_VSYNCEND
		MOVLW		B'00000101'
		XORWF		PORTA, F
	ELSE
		NOP
		BCF		PORTA, HSYNC_PIN
	ENDIF
	;   End of falling edge
		ENDM

CALIBRATION	MACRO		COUNT ; before: 4 cycles, after: 5 cycles
		LOCAL		LOOP, NOT_ZERO, ZERO
		MOVLW		COUNT
		MOVWF		TEMP0
LOOP:		HSYNC		HSYNC_TYPE_CALIB, 0
		DECFSZ		TEMP0, F
		GOTO		NOT_ZERO
		GOTO		ZERO
NOT_ZERO:	CALL		NOP10CYCLES
		NOP
		NOP
		NOP
		GOTO		LOOP
ZERO:		NOP
		ENDM
		
SEND_DATA	MACRO		; before: 15 cycles, after: 6 cycles
		LOCAL		LOOP, NOT_ONE_LINE, ONE_LINE, NOT_LAST, LAST_END, NOT_ZERO, ZERO
		MOVLW		H'0A'
		MOVWF		TILE_ROW
		MOVLW		TILE_MAP
		MOVWF		MAP_INDEX
		MOVLW		D'48'
		MOVWF		TEMP0
LOOP:		MOVLW		SCREEN_BUFFER0
		BTFSS		CURRENT_BUFFER, 0
		MOVLW		SCREEN_BUFFER1
		MOVWF		FSR
		MOVWF		CURRENT_BUFFER_ADDR
		MOVFW		MAP_INDEX
		MOVWF		MAP_INDEX_TEMP
		HSYNC		HSYNC_TYPE_PRE_DRAW, 0
		CALL		NOP10CYCLES
		CALL		NOP8CYCLES
		HSYNC		HSYNC_TYPE_DRAW, 0
		CALL		NOP10CYCLES
		CALL		NOP8CYCLES
		HSYNC		HSYNC_TYPE_PRE_DRAW, 1
		CALL		NOP10CYCLES
		CALL		NOP8CYCLES
		HSYNC		HSYNC_TYPE_DRAW, 0
		CALL		NOP10CYCLES
		CALL		NOP8CYCLES
		HSYNC		HSYNC_TYPE_PRE_DRAW, 2
		CALL		NOP10CYCLES
		CALL		NOP8CYCLES
		HSYNC		HSYNC_TYPE_DRAW, 0
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		INCF		SPRITE0, F
		INCF		SPRITE1, F
		INCF		SPRITE2, F
		INCF		SPRITE3, F
		INCF		SPRITE4, F
		INCF		SPRITE5, F
		HSYNC		HSYNC_TYPE_NOP, 0
		CALL		NOP10CYCLES
		CALL		NOP8CYCLES
		HSYNC		HSYNC_TYPE_DRAW, 0
		CALL		NOP10CYCLES
		CALL		NOP8CYCLES
		HSYNC		HSYNC_TYPE_NOP, 0
		MOVFW		TILE_ROW_BACK
		MOVWF		TILE_ROW
		DECF		TEMP0, W
		BTFSS		STATUS, Z
		GOTO		NOT_LAST
		MOVLW		D'48'
		SUBWF		SPRITE0
		SUBWF		SPRITE1
		SUBWF		SPRITE2
		SUBWF		SPRITE3
		SUBWF		SPRITE4
		SUBWF		SPRITE5
		GOTO		LAST_END
NOT_LAST:
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
LAST_END:
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_DRAW, 0
		DECFSZ		TEMP0, F
		GOTO		NOT_ZERO
		GOTO		ZERO
NOT_ZERO:	MOVLW		H'01'
		XORWF		CURRENT_BUFFER
		NOP
		NOP
		NOP
		NOP
		GOTO		LOOP
ZERO:		MOVLW		H'01'
		XORWF		CURRENT_BUFFER
		ENDM

DRAW_DISPLAY
		HSYNC		HSYNC_TYPE_VSYNCSTART, 0
		CALL		NOP10CYCLES
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_VSYNCEND, 0
		CALL		NOP10CYCLES
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		CALIBRATION	D'33'
		SEND_DATA
		BTFSS		DRAWED_PAST, 0
		CLRF		FILL_COLOR
		BSF		DRAWED_PAST, 0
		MOVLW		H'01'
		ADDWF		SPRITE1, F
		BTFSC		STATUS, DC
		ADDWF		SPRITE0, F
		MOVLW		-H'0A'
		BTFSC		STATUS, DC
		MOVWF		SPRITE0
		CALIBRATION	H'0A'
		MOVLW		H'01'
		ADDWF		SPRITE1 + 1, F
		BTFSC		STATUS, DC
		ADDWF		SPRITE0 + 1, F
		MOVLW		H'05'
		BTFSC		STATUS, DC
		MOVWF		SPRITE0 + 1
		NOP
		NOP
		NOP
		NOP
		GOTO		DRAW_DISPLAY
		
NOP8CYCLES	NOP
		NOP
		NOP
		NOP
		RETURN
		
NOP10CYCLES	NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		RETURN
		
BLACK		EQU	H'00'
DARKBLUE	EQU	H'01'
DARKGREEN	EQU	H'02'
DARKCYAN	EQU	H'03'
DARKRED		EQU	H'04'
DARKPURPLE	EQU	H'05'
DARKYELLOW	EQU	H'06'
DARKGRAY	EQU	H'07'
GRAY		EQU	H'08'
BLUE		EQU	H'09'
GREEN		EQU	H'0A'
CYAN		EQU	H'0B'
RED		EQU	H'0C'
PURPLE		EQU	H'0D'
YELLOW		EQU	H'0E'
WHITE		EQU	H'0F'
	
IMAGE_LOADER	MACRO	    TILE
		MOVLW	    high TILE
		MOVWF	    PCLATH
		MOVFW	    TILE_ROW
		ADDWF	    PCL, F
		ENDM

IMAGE		MACRO	    C0, C1, C2, C3, C4, C5 ; 17 instructions
		MOVLW	    (C0 << 4) | C1
		MOVWF	    INDF
		INCF	    FSR, F
		MOVLW	    (C2 << 4) | C3
		MOVWF	    INDF
		INCF	    FSR, F
		MOVLW	    (C4 << 4) | C5
		MOVWF	    INDF
		INCF	    FSR, F
		RETURN
		ENDM

		INCLUDE	    "TILES.INC"
		
		END