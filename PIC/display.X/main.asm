	LIST	P=PIC16F648A
	INCLUDE	<P16F648A.INC>
	__CONFIG 	_CP_OFF & _MCLRE_OFF & _PWRTE_OFF & _WDT_OFF & _HS_OSC & _LVP_OFF & _BOREN_OFF    

; PIN definition
VSYNC_PIN	EQU		H'00'
HSYNC_PIN	EQU		H'02'
	
; Bank 0 and 2
SCREEN_BUFFER	EQU		H'20'	; 0x20-42

; Bank 1
TILE_MAP	EQU		H'A0'	; 0xA0-D3
	
; ALL BANK
TEMP0		EQU		H'70'
TEMP1		EQU		H'71'
TILE_ROW	EQU		H'72'

COLORBAR	MACRO
	VARIABLE I
I = 0
	WHILE	I < D'45'
		MOVLW	((I & H'0F') << 4) | ((I + 1) & H'0F')
		MOVWF	SCREEN_BUFFER + I / 2
I += 2
	ENDW
		ENDM

CLEAR_SCREENBUFFER  MACRO
		LOCAL		LOOP
		MOVLW		SCREEN_BUFFER
		MOVWF		FSR
		MOVLW		H'23'
		MOVWF		TEMP0
LOOP:
		CLRF		INDF
		INCF		FSR, F
		DECFSZ		TEMP0, F
		GOTO		LOOP
		ENDM
		
CLEAR_TILEMAP	MACRO
		LOCAL		LOOP
		MOVLW		TILE_MAP
		MOVWF		FSR
		CLRF		TEMP1
		MOVLW		H'40'
		MOVWF		TEMP0
LOOP:
		MOVFW		TEMP1
		MOVWF		INDF
		INCF		TEMP1, F
		ADDLW		-H'02'
		BTFSC		STATUS, Z
		CLRF		TEMP1
		INCF		FSR, F
		DECFSZ		TEMP0, F
		GOTO		LOOP
		ENDM
	
		ORG		H'0000'		;Reset Start
INIT		CLRF		PORTA
		MOVLW		H'07'
		MOVWF		CMCON
		BSF		STATUS,RP0	;Set Bank 1
		MOVLW		B'00100000'
		MOVWF		TRISA		;Set RA7-5 to Output and RA4-0 to Input 
		CLRF		TRISB		;Set Port-B to all Output
		MOVLW		H'3F'
		MOVWF		PR2
		
		BCF		STATUS,RP0	;Set Bank 0
		MOVLW		B'00000001'
		MOVWF		PORTA
		CLRF		PORTB
		
		MOVLW		B'00001100'
		IORWF		CCP1CON
		
		BSF		T2CON, TMR2ON
		;MOVLW		H'0F'
		MOVLW		H'00'
		MOVWF		CCPR1L
		;GOTO		$
		
		MOVLW		B'11000000'
		IORWF		INTCON, F
		
		BSF		RCSTA, SPEN
		BSF		STATUS, RP0
		BSF		TRISB, 1
		BSF		TRISB, 2
		BCF		TXSTA, SYNC
		MOVLW		D'207'
		MOVWF		SPBRG
		BSF		TXSTA, TXEN
		BCF		STATUS, RP0
		
		CLEAR_SCREENBUFFER
		CLEAR_TILEMAP

HSYNC_TYPE_VSYNCSTART	EQU	H'00'
HSYNC_TYPE_VSYNCEND	EQU	H'01'
HSYNC_TYPE_CALIB	EQU	H'02'
HSYNC_TYPE_PRE_DRAW	EQU	H'03'
HSYNC_TYPE_DRAW		EQU	H'04'
HSYNC_TYPE_END		EQU	H'05'
HSYNC_TYPE_NOP		EQU	H'06'
HSYNC		MACRO		TYPE	; Wait 14 instructions after SYNC.
	variable	I
	IF	TYPE == HSYNC_TYPE_VSYNCSTART
		MOVLW		B'0000101'
		XORWF		PORTA, F
	ELSE
		BSF		PORTA, HSYNC_PIN
		NOP
	ENDIF
	IF	TYPE == HSYNC_TYPE_PRE_DRAW
		MOVLW		SCREEN_BUFFER
		MOVWF		FSR
	ELSE
		NOP
		NOP
	ENDIF
	NOP
I = 0
	IF	TYPE == HSYNC_TYPE_DRAW
	WHILE	I < D'20'
		MOVFW		SCREEN_BUFFER + I
		MOVWF		PORTB
		SWAPF		SCREEN_BUFFER + I, W
		MOVWF		PORTB
I += 1
	ENDW
		NOP
		CLRF		PORTB
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
	ELSE
	IF	TYPE == HSYNC_TYPE_PRE_DRAW
I = 0
		BSF		STATUS, RP0
	WHILE	I < D'3'
		MOVLW		high TILE_TABLE
		MOVWF		PCLATH
		MOVFW		TILE_MAP + I
		CALL		TILE_TABLE
I += 1
	ENDW
		CALL		TIME10MICRO
		NOP
		NOP
		NOP
		NOP
		BCF		STATUS, RP0
	ELSE
	WHILE	I < D'9'
		CALL		TIME10MICRO
I += 1
	ENDW
		NOP
	ENDIF
	ENDIF
		NOP
		NOP
	IF	TYPE == HSYNC_TYPE_VSYNCEND
		MOVLW		B'00000101'
		XORWF		PORTA, F
	ELSE
		NOP
		BCF		PORTA, HSYNC_PIN
	ENDIF
		ENDM

CALIBRATION	MACRO		COUNT
		LOCAL		LOOP, NOT_ZERO, ZERO
		MOVLW		COUNT
		MOVWF		TEMP0
LOOP:		HSYNC		HSYNC_TYPE_CALIB
		DECFSZ		TEMP0, F
		GOTO		NOT_ZERO
		GOTO		ZERO
NOT_ZERO:	NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		GOTO		LOOP
ZERO:		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		ENDM

REPEAT_HSYNC	MACRO		COUNT, TYPE
		LOCAL		LOOP, NOT_ZERO, ZERO
		MOVLW		COUNT
		MOVWF		TEMP0
LOOP:		HSYNC		TYPE
		DECFSZ		TEMP0, F
		GOTO		NOT_ZERO
		GOTO		ZERO
NOT_ZERO:	NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		GOTO		LOOP
ZERO:		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		ENDM
		
SEND_DATA	MACRO
		LOCAL		LOOP, NOT_ZERO, ZERO
		CLRF		TILE_ROW
		MOVLW		D'48'
		MOVWF		TEMP0
LOOP:		
		HSYNC		HSYNC_TYPE_PRE_DRAW
		CALL		TIME10MICRO
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_DRAW
		CALL		TIME10MICRO
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_NOP
		CALL		TIME10MICRO
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_DRAW
		CALL		TIME10MICRO
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_NOP
		CALL		TIME10MICRO
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_DRAW
		CALL		TIME10MICRO
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_NOP
		CALL		TIME10MICRO
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_DRAW
		CALL		TIME10MICRO
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_NOP
		MOVLW		H'0A'
		ADDWF		TILE_ROW
		MOVLW		D'60'
		SUBWF		TILE_ROW, W
		BTFSC		STATUS, Z
		CLRF		TILE_ROW
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_DRAW
		DECFSZ		TEMP0, F
		GOTO		NOT_ZERO
		GOTO		ZERO
NOT_ZERO:	NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		GOTO		LOOP
ZERO:		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		ENDM

DRAW_DISPLAY
		HSYNC		HSYNC_TYPE_VSYNCSTART
		CALL		TIME10MICRO
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_VSYNCEND
		NOP
		NOP
		CALIBRATION	D'33'
		NOP
		SEND_DATA
		NOP
		NOP
		CALIBRATION	H'0A'
		GOTO		DRAW_DISPLAY
		
TIME10MICRO	NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		RETURN
		
BLACK		EQU	H'00'
DARKBLUE	EQU	H'01'
DARKGREEN	EQU	H'02'
DARKCYAN	EQU	H'03'
DARKRED		EQU	H'04'
DARKPURPLE	EQU	H'05'
DARKYELLOW	EQU	H'06'
DARKGRAY	EQU	H'07'
GRAY		EQU	H'08'
BLUE		EQU	H'09'
GREEN		EQU	H'0A'
CYAN		EQU	H'0B'
RED		EQU	H'0C'
PURPLE		EQU	H'0D'
YELLOW		EQU	H'0E'
WHITE		EQU	H'0F'
	
IMAGE_LOADER	MACRO
		MOVLW	    high CHARACTER_ZERO
		MOVWF	    PCLATH
		MOVFW	    TILE_ROW
		ADDWF	    PCL, F
		ENDM

IMAGE		MACRO	    C0, C1, C2, C3, C4, C5 ; 17 instructions
		MOVLW	    (C0 << 4) | C1
		MOVWF	    INDF
		INCF	    FSR, F
		MOVLW	    (C2 << 4) | C3
		MOVWF	    INDF
		INCF	    FSR, F
		MOVLW	    (C4 << 4) | C5
		MOVWF	    INDF
		INCF	    FSR, F
		RETURN
		ENDM

TILE_TABLE
		ADDWF	    PCL, F
		GOTO	    CHARACTER_ZERO
		GOTO	    CHARACTER_ONE
		GOTO	    COLOR_TILE

ORG		H'400'
CHARACTER_ZERO
		IMAGE_LOADER
		IMAGE	    BLACK, WHITE, WHITE, BLACK, BLACK, BLACK
		IMAGE	    WHITE, BLACK, BLACK, WHITE, BLACK, BLACK
		IMAGE	    WHITE, BLACK, BLACK, WHITE, BLACK, BLACK
		IMAGE	    WHITE, BLACK, BLACK, WHITE, BLACK, BLACK
		IMAGE	    BLACK, WHITE, WHITE, BLACK, BLACK, BLACK
		IMAGE	    BLACK, BLACK, BLACK, BLACK, BLACK, BLACK
		
CHARACTER_ONE
		IMAGE_LOADER
		IMAGE	    BLACK, WHITE, WHITE, BLACK, BLACK, BLACK
		IMAGE	    WHITE, BLACK, WHITE, BLACK, BLACK, BLACK
		IMAGE	    BLACK, BLACK, WHITE, BLACK, BLACK, BLACK
		IMAGE	    BLACK, BLACK, WHITE, BLACK, BLACK, BLACK
		IMAGE	    WHITE, WHITE, WHITE, WHITE, WHITE, BLACK
		IMAGE	    BLACK, BLACK, BLACK, BLACK, BLACK, BLACK
		
COLOR_TILE
		IMAGE_LOADER
		IMAGE	    BLACK, DARKBLUE, DARKGREEN, DARKCYAN, DARKRED, DARKPURPLE
		IMAGE	    DARKYELLOW, DARKGRAY , BLACK, BLACK, GRAY, BLUE
		IMAGE	    GREEN, CYAN, RED, PURPLE, YELLOW, WHITE
		IMAGE	    BLACK, DARKBLUE, DARKGREEN, DARKCYAN, DARKRED, DARKPURPLE
		IMAGE	    DARKYELLOW, DARKGRAY , BLACK, BLACK, GRAY, BLUE
		IMAGE	    GREEN, CYAN, RED, PURPLE, YELLOW, WHITE
		
		END