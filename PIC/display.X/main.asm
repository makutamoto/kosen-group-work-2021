	LIST	P=PIC16F648A
	INCLUDE	<P16F648A.INC>
	__CONFIG 	_CP_OFF & _MCLRE_OFF & _PWRTE_OFF & _WDT_OFF & _HS_OSC & _LVP_OFF & _BOREN_OFF    

; PIN definition
VSYNC_PIN	EQU		H'00'
HSYNC_PIN	EQU		H'02'
	
; Bank 0 and 2
SCREEN_BUFFER0	EQU		H'20'	; 0x20-3D
SCREEN_BUFFER1	EQU		H'3E'	; 0x20-3D

; Bank 1
TILE_MAP	EQU		H'A0'	; 0xA0-EF
	
; ALL BANK
TEMP0		EQU		H'70'
TEMP1		EQU		H'71'
TILE_ROW	EQU		H'72'
MAP_INDEX	EQU		H'73'
MAP_INDEX_TEMP	EQU		H'74'
FSR_BACK	EQU		H'75'
FILL_COLOR	EQU		H'76' ; B'BBBB000A' A: Flag, B: Color
CURRENT_BUFFER	EQU		H'77'
DRAWED_PAST	EQU		H'88'

COLORBAR	MACRO
	VARIABLE I
I = 0
	WHILE	I < D'45'
		MOVLW	((I & H'0F') << 4) | ((I + 1) & H'0F')
		MOVWF	SCREEN_BUFFER + I / 2
I += 2
	ENDW
		ENDM
		
CLEAR_TILEMAP	MACRO
		LOCAL		LOOP
		MOVLW		TILE_MAP
		MOVWF		FSR
		CLRF		TEMP1
		MOVLW		H'80'
		MOVWF		TEMP0
LOOP:
		MOVFW		TEMP1
		MOVWF		INDF
		INCF		TEMP1, F
		ADDLW		-H'02'
		BTFSC		STATUS, Z
		CLRF		TEMP1
		INCF		FSR, F
		DECFSZ		TEMP0, F
		GOTO		LOOP
		ENDM
	
		ORG		H'0000'		;Reset Start
INIT		MOVLW		H'07'
		MOVWF		CMCON
		BSF		STATUS,RP0	;Set Bank 1
		MOVLW		B'00100000'
		MOVWF		TRISA		;Set RA7-5 to Output and RA4-0 to Input 
		CLRF		TRISB		;Set Port-B to all Output
		MOVLW		H'3F'
		MOVWF		PR2
		
		BSF		RCSTA, SPEN
		BSF		STATUS, RP0
		BSF		TRISB, 1
		BSF		TRISB, 2
		BCF		TXSTA, SYNC
		MOVLW		D'207'
		MOVWF		SPBRG
		BSF		TXSTA, TXEN
		
		BCF		STATUS,RP0	;Set Bank 0
		MOVLW		B'00000001'
		MOVWF		PORTA
		CLRF		PORTB
		
		;MOVLW		B'00001100'
		;IORWF		CCP1CON
		
		;BSF		T2CON, TMR2ON
		;MOVLW		H'0F'
		;MOVLW		H'00'
		;MOVWF		CCPR1L
		;GOTO		$
		
		;MOVLW		B'11000000'
		;IORWF		INTCON, F
		
		;BCF		STATUS, RP0
		
		CLRF		CURRENT_BUFFER
		
		CLEAR_TILEMAP
		
		CLRF		DRAWED_PAST
		MOVLW		H'01'
		MOVWF		FILL_COLOR

CALC_PIXELS	MACRO
		MOVLW		high TILE_TABLE
		MOVWF		PCLATH
		MOVFW		FSR
		MOVWF		FSR_BACK
		MOVFW		MAP_INDEX_TEMP
		MOVWF		FSR
		MOVFW		INDF
		MOVWF		TEMP1
		INCF		MAP_INDEX_TEMP, F
		MOVFW		FSR_BACK
		MOVWF		FSR
		MOVFW		TEMP1
		CALL		TILE_TABLE
		ENDM
		
DRAW_BUFFER	MACRO	BUFFER
	VARIABLE I
		NOP
		NOP
I = 0
	WHILE	I < D'30'
		MOVFW		BUFFER + I
		MOVWF		PORTB
		SWAPF		BUFFER + I, W
		MOVWF		PORTB
I += 1
	ENDW
		NOP
		CLRF		PORTB
		ENDM
		
HSYNC_TYPE_VSYNCSTART	EQU	H'00'
HSYNC_TYPE_VSYNCEND	EQU	H'01'
HSYNC_TYPE_CALIB	EQU	H'02'
HSYNC_TYPE_PRE_DRAW	EQU	H'03' ; ARG1: PREDRAW_NUM
HSYNC_TYPE_DRAW		EQU	H'04'
HSYNC_TYPE_END		EQU	H'05'
HSYNC_TYPE_NOP		EQU	H'06'
HSYNC		MACRO		TYPE, ARG0	; Wait 18 instructions after SYNC.
	;   Start of rising edge
	LOCAL		FILL_DRAW, DRAW_BUFFER1, END_DRAW
	variable	I
	IF	TYPE == HSYNC_TYPE_VSYNCSTART
		MOVLW		B'0000101'
		XORWF		PORTA, F
	ELSE
		NOP	
		BSF		PORTA, HSYNC_PIN
	ENDIF
	;   End of falling edge
	
	;	free space 139 cycles
	IF	TYPE == HSYNC_TYPE_PRE_DRAW
		BSF		STATUS, RP0
	IF	ARG0 < 2
		CALC_PIXELS
		CALC_PIXELS
		CALC_PIXELS
		CALC_PIXELS
		CLRF		PCLATH
	ELSE
		CALC_PIXELS
		CALC_PIXELS
		CLRF		PCLATH
		CALL		NOP10CYCLES
		CALL		NOP10CYCLES
		CALL		NOP10CYCLES
		CALL		NOP10CYCLES
		CALL		NOP10CYCLES
		CALL		NOP10CYCLES
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
	ENDIF
		BCF		STATUS, RP0
	ELSE
	IF	TYPE == HSYNC_TYPE_DRAW
		NOP
		NOP
		NOP
		NOP
		NOP
		BTFSC		FILL_COLOR, 0
		GOTO		FILL_DRAW
		BTFSC		CURRENT_BUFFER, 0
		GOTO		DRAW_BUFFER1
		NOP
		DRAW_BUFFER	SCREEN_BUFFER0
		NOP
		NOP
		GOTO		END_DRAW
DRAW_BUFFER1:
		DRAW_BUFFER	SCREEN_BUFFER1
		NOP
		NOP
		GOTO		END_DRAW
FILL_DRAW:
		NOP
		NOP
		NOP
		NOP
		MOVFW		FILL_COLOR
		MOVWF		PORTB
I = 0
	WHILE	I < D'11'
		CALL		NOP10CYCLES
I += 1
	ENDW
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		CLRF		PORTB
		NOP
		NOP
		NOP
		NOP
		
END_DRAW:	
		NOP
	ELSE
	WHILE	I < D'13'
		CALL		NOP10CYCLES
I += 1
	ENDW
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
	ENDIF
	ENDIF
	
	;   Start of falling edge
	IF	TYPE == HSYNC_TYPE_VSYNCEND
		MOVLW		B'00000101'
		XORWF		PORTA, F
	ELSE
		NOP
		BCF		PORTA, HSYNC_PIN
	ENDIF
	;   End of falling edge
		ENDM

CALIBRATION	MACRO		COUNT ; before: 4 cycles, after: 5 cycles
		LOCAL		LOOP, NOT_ZERO, ZERO
		MOVLW		COUNT
		MOVWF		TEMP0
LOOP:		HSYNC		HSYNC_TYPE_CALIB, 0
		DECFSZ		TEMP0, F
		GOTO		NOT_ZERO
		GOTO		ZERO
NOT_ZERO:	CALL		NOP10CYCLES
		NOP
		NOP
		NOP
		GOTO		LOOP
ZERO:		NOP
		ENDM
		
SEND_DATA	MACRO		; before: 14 cycles, after: 6 cycles
		LOCAL		LOOP, NOT_ONE_LINE, ONE_LINE, NOT_ZERO, ZERO
		MOVLW		H'0A'
		MOVWF		TILE_ROW
		MOVLW		TILE_MAP
		MOVWF		MAP_INDEX
		MOVLW		D'48'
		MOVWF		TEMP0
LOOP:		MOVLW		SCREEN_BUFFER0
		BTFSS		CURRENT_BUFFER, 0
		MOVLW		SCREEN_BUFFER1
		MOVWF		FSR
		MOVFW		MAP_INDEX
		MOVWF		MAP_INDEX_TEMP
		HSYNC		HSYNC_TYPE_PRE_DRAW, 0
		CALL		NOP10CYCLES
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_DRAW, 0
		CALL		NOP10CYCLES
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_PRE_DRAW, 1
		CALL		NOP10CYCLES
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_DRAW, 0
		CALL		NOP10CYCLES
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_PRE_DRAW, 2
		CALL		NOP10CYCLES
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_DRAW, 0
		CALL		NOP10CYCLES
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_NOP, 0
		CALL		NOP10CYCLES
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_DRAW, 0
		CALL		NOP10CYCLES
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_NOP, 0
		MOVLW		H'0A'
		ADDWF		TILE_ROW
		MOVLW		D'60'
		SUBWF		TILE_ROW, W
		BTFSS		STATUS, Z
		GOTO		NOT_ONE_LINE
		CLRF		TILE_ROW
		MOVLW		H'0A'
		ADDWF		MAP_INDEX, F
		MOVLW		H'F0'
		SUBWF		MAP_INDEX, W
		MOVLW		H'A0'
		BTFSC		STATUS, Z
		MOVWF		MAP_INDEX
		GOTO		ONE_LINE
NOT_ONE_LINE:
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
ONE_LINE:	
		NOP
		NOP
		HSYNC		HSYNC_TYPE_DRAW, 0
		DECFSZ		TEMP0, F
		GOTO		NOT_ZERO
		GOTO		ZERO
NOT_ZERO:	MOVLW		H'01'
		XORWF		CURRENT_BUFFER
		NOP
		NOP
		NOP
		NOP
		NOP
		GOTO		LOOP
ZERO:		MOVLW		H'01'
		XORWF		CURRENT_BUFFER
		ENDM

DRAW_DISPLAY
		HSYNC		HSYNC_TYPE_VSYNCSTART, 0
		CALL		NOP10CYCLES
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		HSYNC		HSYNC_TYPE_VSYNCEND, 0
		CALL		NOP10CYCLES
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		CALIBRATION	D'33'
		NOP
		SEND_DATA
		BTFSS		DRAWED_PAST, 0
		CLRF		FILL_COLOR
		BSF		DRAWED_PAST, 0
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		CALIBRATION	H'0A'
		CALL		NOP10CYCLES
		NOP
		GOTO		DRAW_DISPLAY
		
NOP10CYCLES	NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		RETURN
		
BLACK		EQU	H'00'
DARKBLUE	EQU	H'01'
DARKGREEN	EQU	H'02'
DARKCYAN	EQU	H'03'
DARKRED		EQU	H'04'
DARKPURPLE	EQU	H'05'
DARKYELLOW	EQU	H'06'
DARKGRAY	EQU	H'07'
GRAY		EQU	H'08'
BLUE		EQU	H'09'
GREEN		EQU	H'0A'
CYAN		EQU	H'0B'
RED		EQU	H'0C'
PURPLE		EQU	H'0D'
YELLOW		EQU	H'0E'
WHITE		EQU	H'0F'
	
IMAGE_LOADER	MACRO
		MOVLW	    high CHARACTER_ZERO
		MOVWF	    PCLATH
		MOVFW	    TILE_ROW
		ADDWF	    PCL, F
		ENDM

IMAGE		MACRO	    C0, C1, C2, C3, C4, C5 ; 17 instructions
		MOVLW	    (C0 << 4) | C1
		MOVWF	    INDF
		INCF	    FSR, F
		MOVLW	    (C2 << 4) | C3
		MOVWF	    INDF
		INCF	    FSR, F
		MOVLW	    (C4 << 4) | C5
		MOVWF	    INDF
		INCF	    FSR, F
		RETURN
		ENDM

		ORG		H'800'
TILE_TABLE
		ADDWF	    PCL, F
		GOTO	    CHARACTER_ZERO
		GOTO	    CHARACTER_ONE
		GOTO	    COLOR_TILE

		ORG		H'840'
CHARACTER_ZERO
		IMAGE_LOADER
		IMAGE	    BLACK, WHITE, WHITE, BLACK, BLACK, BLACK
		IMAGE	    WHITE, BLACK, BLACK, WHITE, BLACK, BLACK
		IMAGE	    WHITE, BLACK, BLACK, WHITE, BLACK, BLACK
		IMAGE	    WHITE, BLACK, BLACK, WHITE, BLACK, BLACK
		IMAGE	    BLACK, WHITE, WHITE, BLACK, BLACK, BLACK
		IMAGE	    BLACK, BLACK, BLACK, BLACK, BLACK, BLACK
		
CHARACTER_ONE
		IMAGE_LOADER
		IMAGE	    BLACK, WHITE, WHITE, BLACK, BLACK, BLACK
		IMAGE	    WHITE, BLACK, WHITE, BLACK, BLACK, BLACK
		IMAGE	    BLACK, BLACK, WHITE, BLACK, BLACK, BLACK
		IMAGE	    BLACK, BLACK, WHITE, BLACK, BLACK, BLACK
		IMAGE	    WHITE, WHITE, WHITE, WHITE, WHITE, BLACK
		IMAGE	    BLACK, BLACK, BLACK, BLACK, BLACK, BLACK
		
COLOR_TILE
		IMAGE_LOADER
		IMAGE	    BLACK, DARKBLUE, DARKGREEN, DARKCYAN, DARKRED, DARKPURPLE
		IMAGE	    DARKYELLOW, DARKGRAY , BLACK, BLACK, GRAY, BLUE
		IMAGE	    GREEN, CYAN, RED, PURPLE, YELLOW, WHITE
		IMAGE	    BLACK, DARKBLUE, DARKGREEN, DARKCYAN, DARKRED, DARKPURPLE
		IMAGE	    DARKYELLOW, DARKGRAY , BLACK, BLACK, GRAY, BLUE
		IMAGE	    GREEN, CYAN, RED, PURPLE, YELLOW, WHITE
		
		END