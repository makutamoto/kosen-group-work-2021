	LIST	P=PIC16F648A
	INCLUDE	<P16F648A.INC>
	__CONFIG 	_CP_OFF & _MCLRE_OFF & _PWRTE_OFF & _WDT_OFF & _HS_OSC & _LVP_OFF & _BOREN_OFF    

	INCLUDE "TABLE.INC"
	
; Constants
PADDLE0_HIGH	EQU		D'80'
PADDLE0_LOW	EQU		D'83'
PADDLE1_HIGH	EQU		D'86'
PADDLE1_LOW	EQU		D'89'
BALL		EQU		D'92'
		
TABLE_TOP	EQU		D'7'
TABLE_BOTTOM	EQU		D'41'
TABLE_LEFT	EQU		D'0'
TABLE_RIGHT	EQU		D'27'
	
; Pin definitions
;PORTB
MASTER_BUSY	EQU		H'05'
	
; bank 0
STATUS_TEMP	EQU		H'20'
	
; ALL BANK
ARG0		EQU		H'70'
ARG1		EQU		H'71'
TEMP0		EQU		H'72'
TEMP1		EQU		H'73'
SLAVE_FREE	EQU		H'74'
W_TEMP		EQU		H'75'
PADDLE0_Y	EQU		H'76'
PADDLE1_Y	EQU		H'77'
INT_TEMP0	EQU		H'78'
INT_TEMP1	EQU		H'79'
BALL_Y		EQU		H'7A'
BALL_X		EQU		H'7B'
BALL_VEL_Y	EQU		H'7C'
BALL_VEL_X	EQU		H'7D'
BALL_COUNTER	EQU		H'7E'
		

		ORG		H'0000'
		GOTO		INIT
		ORG		H'0004'
INTERRUPT
		MOVWF		W_TEMP
		SWAPF		STATUS, W
		BCF		STATUS, RP0
		MOVWF		STATUS_TEMP
		
		BTFSC		INTCON, INTF
		GOTO		INT_MATCH
		BTFSC		PIR1, RCIF
		GOTO		RC_MATCH
		GOTO		INTERRUPT_END
INT_MATCH	
		BSF		SLAVE_FREE, 0
		BCF		INTCON, INTF
		GOTO		INTERRUPT_END
RC_MATCH
		MOVFW		RCREG
		MOVWF		INT_TEMP0
		
		BTFSC		INT_TEMP0, 7
		INCF		PADDLE0_Y, F
		BTFSC		INT_TEMP0, 6
		DECF		PADDLE0_Y, F
		
		MOVLW		TABLE_TOP
		SUBWF		PADDLE0_Y, W
		MOVWF		INT_TEMP1
		MOVLW		TABLE_TOP
		BTFSC		INT_TEMP1, 7
		MOVWF		PADDLE0_Y
		MOVLW		TABLE_BOTTOM - D'6'
		SUBWF		PADDLE0_Y, W
		MOVLW		TABLE_BOTTOM - D'6'
		BTFSC		STATUS, C
		MOVWF		PADDLE0_Y
		
		BTFSC		INT_TEMP0, 5
		INCF		PADDLE1_Y, F
		BTFSC		INT_TEMP0, 4
		DECF		PADDLE1_Y, F
		
		MOVLW		TABLE_TOP
		SUBWF		PADDLE1_Y, W
		MOVWF		INT_TEMP1
		MOVLW		TABLE_TOP
		BTFSC		INT_TEMP1, 7
		MOVWF		PADDLE1_Y
		MOVLW		TABLE_BOTTOM - 6
		SUBWF		PADDLE1_Y, W
		MOVLW		TABLE_BOTTOM - 6
		BTFSC		STATUS, C
		MOVWF		PADDLE1_Y
		
		GOTO		INTERRUPT_END
INTERRUPT_END
		SWAPF		STATUS_TEMP, W
		MOVWF		STATUS
		SWAPF		W_TEMP, F
		SWAPF		W_TEMP, W
		
		RETFIE
INIT		
		MOVLW		H'07'
		MOVWF		CMCON
		BSF		STATUS,RP0
		CLRF		TRISA
		MOVLW		B'00000111'
		MOVWF		TRISB
		MOVLW		H'3F'
		MOVWF		PR2
		
		MOVLW		B'11010111'
		MOVWF		OPTION_REG
		
		MOVLW		B'00100000'
		MOVWF		PIE1
		
		BCF		STATUS, RP0
		CLRF		PORTA
		MOVLW		B'00100000'
		MOVWF		PORTB
		
		CLRF		TMR0
		
		MOVLW		B'00001100'
		IORWF		CCP1CON
		
		BSF		T2CON, TMR2ON
		MOVLW		H'0F'
		MOVWF		CCPR1L
		
		MOVLW		B'11010000'
		MOVWF		INTCON
		
		BSF		RCSTA, SPEN
		BSF		STATUS, RP0
		BCF		TXSTA, SYNC
		BSF		TXSTA, BRGH
		CLRF		SPBRG
		BSF		TXSTA, TXEN
		BCF		STATUS, RP0
		BSF		RCSTA, CREN
		
		CLRF		SLAVE_FREE
		
		CLRF		BALL_COUNTER
		
		GOTO		START

SEND_COMMAND	; ARG0: address, ARG1: command
		BTFSS		SLAVE_FREE, 0
		GOTO		$ - 1
		BCF		INTCON, INTE
		BCF		PORTB, MASTER_BUSY
		MOVFW		ARG0
		MOVWF		TXREG
		MOVFW		ARG1
		MOVWF		TXREG
		BSF		STATUS, RP0
		BTFSS		TXSTA, TRMT
		GOTO		$ - 1
		BCF		STATUS, RP0
		BSF		PORTB, MASTER_BUSY
		BCF		INTCON, INTF
		BSF		INTCON, INTE
		BCF		SLAVE_FREE, 0
		RETURN

SET_POS_Y	MACRO		SPRITE, Y
		MOVLW		SPRITE
		MOVWF		ARG0
		MOVFW		Y
		SUBLW		H'00'
		MOVWF		ARG1
		CALL		SEND_COMMAND
		ENDM
		
SET_POS_X	MACRO		SPRITE, X
		MOVLW		SPRITE + 1
		MOVWF		ARG0
		MOVFW		X
		MOVWF		ARG1
		CALL		SEND_COMMAND
		ENDM
		
SET_SPRITE	MACRO		SPRITE, TILE
		MOVLW		SPRITE + 2
		MOVWF		ARG0
		MOVLW		TILE
		MOVWF		ARG1
		CALL		SEND_COMMAND
		ENDM
		
SET_BACKGROUND	MACRO		Y, X, TILE
		MOVLW		Y * D'10' + X
		MOVWF		ARG0
		MOVLW		TILE
		MOVWF		ARG1
		CALL		SEND_COMMAND
		ENDM

		
BACKGROUND
		SET_BACKGROUND	0, 0, TILE_ZERO
		SET_BACKGROUND	0, 1, TILE_TITLE_BACKGROUND
		SET_BACKGROUND	0, 2, TILE_TITLE_BACKGROUND
		SET_BACKGROUND	0, 3, TILE_P
		SET_BACKGROUND	0, 4, TILE_O
		SET_BACKGROUND	0, 5, TILE_N
		SET_BACKGROUND	0, 6, TILE_G
		SET_BACKGROUND	0, 7, TILE_TITLE_BACKGROUND
		SET_BACKGROUND	0, 8, TILE_TITLE_BACKGROUND
		SET_BACKGROUND	0, 9, TILE_ZERO
		
		SET_BACKGROUND	1, 0, TILE_TOP_LEFT_CORNER
		SET_BACKGROUND	1, 1, TILE_TOP
		SET_BACKGROUND	1, 2, TILE_TOP
		SET_BACKGROUND	1, 3, TILE_TOP
		SET_BACKGROUND	1, 4, TILE_TOP_CENTER
		SET_BACKGROUND	1, 5, TILE_TOP
		SET_BACKGROUND	1, 6, TILE_TOP
		SET_BACKGROUND	1, 7, TILE_TOP
		SET_BACKGROUND	1, 8, TILE_TOP
		SET_BACKGROUND	1, 9, TILE_TOP_RIGHT_CORNER
		
		SET_BACKGROUND	2, 0, TILE_LEFT
		SET_BACKGROUND	2, 1, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	2, 2, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	2, 3, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	2, 4, TILE_CENTER
		SET_BACKGROUND	2, 5, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	2, 6, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	2, 7, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	2, 8, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	2, 9, TILE_RIGHT
		
		SET_BACKGROUND	3, 0, TILE_LEFT
		SET_BACKGROUND	3, 1, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	3, 2, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	3, 3, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	3, 4, TILE_CENTER
		SET_BACKGROUND	3, 5, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	3, 6, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	3, 7, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	3, 8, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	3, 9, TILE_RIGHT
		
		SET_BACKGROUND	4, 0, TILE_LEFT
		SET_BACKGROUND	4, 1, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	4, 2, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	4, 3, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	4, 4, TILE_CENTER
		SET_BACKGROUND	4, 5, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	4, 6, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	4, 7, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	4, 8, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	4, 9, TILE_RIGHT
		
		SET_BACKGROUND	5, 0, TILE_LEFT
		SET_BACKGROUND	5, 1, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	5, 2, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	5, 3, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	5, 4, TILE_CENTER
		SET_BACKGROUND	5, 5, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	5, 6, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	5, 7, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	5, 8, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	5, 9, TILE_RIGHT
		
		SET_BACKGROUND	6, 0, TILE_LEFT
		SET_BACKGROUND	6, 1, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	6, 2, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	6, 3, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	6, 4, TILE_CENTER
		SET_BACKGROUND	6, 5, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	6, 6, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	6, 7, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	6, 8, TILE_TABLE_BACKGROUND
		SET_BACKGROUND	6, 9, TILE_RIGHT
		
		SET_BACKGROUND	7, 0, TILE_BOTTOM_LEFT_CORNER
		SET_BACKGROUND	7, 1, TILE_BOTTOM
		SET_BACKGROUND	7, 2, TILE_BOTTOM
		SET_BACKGROUND	7, 3, TILE_BOTTOM
		SET_BACKGROUND	7, 4, TILE_BOTTOM_CENTER
		SET_BACKGROUND	7, 5, TILE_BOTTOM
		SET_BACKGROUND	7, 6, TILE_BOTTOM
		SET_BACKGROUND	7, 7, TILE_BOTTOM
		SET_BACKGROUND	7, 8, TILE_BOTTOM
		SET_BACKGROUND	7, 9, TILE_BOTTOM_RIGHT_CORNER
		RETURN
		
START
		CALL		BACKGROUND
		
		MOVLW		(TABLE_TOP + (TABLE_BOTTOM + D'6')) / D'2' - D'6'
		MOVWF		PADDLE0_Y
		MOVWF		PADDLE1_Y
		
		SET_SPRITE	PADDLE0_HIGH, TILE_PADDLE0
		SET_SPRITE	PADDLE0_LOW, TILE_PADDLE0
		
		SET_SPRITE	PADDLE1_HIGH, TILE_PADDLE1
		SET_SPRITE	PADDLE1_LOW, TILE_PADDLE1
		
		SET_SPRITE	BALL, TILE_BALL
		
		CLRF		TEMP0
		SET_POS_Y	PADDLE0_HIGH, TEMP0
		SET_POS_Y	PADDLE1_HIGH, TEMP0
		MOVLW		H'06'
		MOVWF		TEMP0
		SET_POS_Y	PADDLE0_LOW, TEMP0
		SET_POS_Y	PADDLE1_LOW, TEMP0
		
		CLRF		TEMP0
		SET_POS_X	PADDLE0_HIGH, TEMP0
		SET_POS_X	PADDLE0_LOW, TEMP0
		MOVLW		D'27'
		MOVWF		TEMP0
		SET_POS_X	PADDLE1_HIGH, TEMP0
		SET_POS_X	PADDLE1_LOW, TEMP0
		
		CLRF		BALL_Y
		CLRF		BALL_X
		SET_POS_Y	BALL, BALL_Y
		SET_POS_X	BALL, BALL_X
		
		MOVLW		H'01'
		MOVWF		BALL_VEL_Y
		MOVWF		BALL_VEL_X
LOOP
		BTFSS		INTCON, T0IF
		GOTO		$ - 1
		BCF		INTCON, T0IF
		
		SET_POS_Y	PADDLE0_HIGH, PADDLE0_Y
		MOVLW		H'06'
		ADDWF		PADDLE0_Y, W
		MOVWF		TEMP0
		SET_POS_Y	PADDLE0_LOW, TEMP0
		
		SET_POS_Y	PADDLE1_HIGH, PADDLE1_Y
		MOVLW		H'06'
		ADDWF		PADDLE1_Y, W
		MOVWF		TEMP0
		SET_POS_Y	PADDLE1_LOW, TEMP0
		
		INCF		BALL_COUNTER, F
		MOVLW		H'06'
		SUBWF		BALL_COUNTER, W
		BTFSS		STATUS, Z
		GOTO		LOOP
		
		CLRF		BALL_COUNTER
VERTICAL
		MOVFW		BALL_VEL_Y
		ADDWF		BALL_Y, F
TOP_CHECK
		MOVLW		TABLE_TOP - D'2'
		SUBWF		BALL_Y, W
		MOVWF		TEMP0
		BTFSS		TEMP0, 7
		GOTO		BOTTOM_CHECK
		MOVLW		TABLE_TOP - D'2'
		MOVWF		BALL_Y
		MOVFW		BALL_VEL_Y
		SUBLW		H'00'
		MOVWF		BALL_VEL_Y
BOTTOM_CHECK
		MOVLW		TABLE_BOTTOM + D'2'
		SUBWF		BALL_Y, W
		BTFSS		STATUS, C
		GOTO		HORIZONTAL
		MOVLW		TABLE_BOTTOM + D'2'
		MOVWF		BALL_Y
		MOVFW		BALL_VEL_Y
		SUBLW		H'00'
		MOVWF		BALL_VEL_Y
HORIZONTAL
		MOVFW		BALL_VEL_X
		ADDWF		BALL_X, F
LEFT_CHECK
		MOVLW		TABLE_LEFT
		SUBWF		BALL_X, W
		MOVWF		TEMP0
		BTFSS		TEMP0, 7
		GOTO		RIGHT_CHECK
		MOVLW		TABLE_LEFT
		MOVWF		BALL_X
		MOVFW		BALL_VEL_X
		SUBLW		H'00'
		MOVWF		BALL_VEL_X
RIGHT_CHECK
		MOVLW		TABLE_RIGHT
		SUBWF		BALL_X, W
		BTFSS		STATUS, C
		GOTO		CHECK_END
		MOVLW		TABLE_RIGHT
		MOVWF		BALL_X
		MOVFW		BALL_VEL_X
		SUBLW		H'00'
		MOVWF		BALL_VEL_X
CHECK_END
		SET_POS_Y	BALL, BALL_Y
		SET_POS_X	BALL, BALL_X
		
		GOTO		LOOP

NOP10CYCLES	NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		RETURN
		
		END
