	LIST	P=PIC16F648A
	INCLUDE	<P16F648A.INC>
	__CONFIG 	_CP_OFF & _MCLRE_OFF & _PWRTE_OFF & _WDT_OFF & _HS_OSC & _LVP_OFF & _BOREN_OFF    

; Pin definitions
;PORTB
MASTER_BUSY	EQU		H'05'
	
; bank 0
STATUS_TEMP	EQU		H'20'
	
; ALL BANK
ARG0		EQU		H'70'
ARG1		EQU		H'71'
TEMP0		EQU		H'72'
TEMP1		EQU		H'73'
SLAVE_FREE	EQU		H'74'
W_TEMP		EQU		H'75'
GUN_Y		EQU		H'76'
GUN_X		EQU		H'77'
GUN_FLAGS	EQU		H'78'
INT_TEMP0	EQU		H'79'
INT_TEMP1	EQU		H'7A'
FSR_BACK	EQU		H'7B'
		

		ORG		H'0000'
		GOTO		INIT
		ORG		H'0004'
INTERRUPT
		MOVWF		W_TEMP
		SWAPF		STATUS, W
		BCF		STATUS, RP0
		MOVWF		STATUS_TEMP
		
		BTFSC		INTCON, INTF
		GOTO		INT_MATCH
		BTFSC		PIR1, RCIF
		GOTO		RC_MATCH
		GOTO		INTERRUPT_END
INT_MATCH	
		BSF		SLAVE_FREE, 0
		BCF		INTCON, INTF
		GOTO		INTERRUPT_END
RC_MATCH
		MOVFW		FSR
		MOVWF		FSR_BACK
		
		MOVLW		GUN_Y
		MOVWF		FSR
		MOVFW		RCREG
		MOVWF		INT_TEMP0
		ANDLW		B'11000000'
		MOVWF		INT_TEMP1
		BCF		STATUS, C
		RLF		INT_TEMP1, F
		RLF		INT_TEMP1, F
		RLF		INT_TEMP1, W
		ADDWF		FSR
		MOVLW		B'00111111'
		ANDWF		INT_TEMP0, W
		MOVWF		INDF
		
		MOVFW		FSR_BACK
		MOVWF		FSR
		GOTO		INTERRUPT_END
INTERRUPT_END
		SWAPF		STATUS_TEMP, W
		MOVWF		STATUS
		SWAPF		W_TEMP, F
		SWAPF		W_TEMP, W
		
		RETFIE
INIT		
		MOVLW		H'07'
		MOVWF		CMCON
		BSF		STATUS,RP0
		CLRF		TRISA
		MOVLW		B'00000111'
		MOVWF		TRISB
		MOVLW		H'3F'
		MOVWF		PR2
		
		MOVLW		B'11010111'
		MOVWF		OPTION_REG
		
		MOVLW		B'00100000'
		MOVWF		PIE1
		
		BCF		STATUS, RP0
		CLRF		PORTA
		MOVLW		B'00100000'
		MOVWF		PORTB
		
		CLRF		TMR0
		
		MOVLW		B'00001100'
		IORWF		CCP1CON
		
		BSF		T2CON, TMR2ON
		MOVLW		H'0F'
		MOVWF		CCPR1L
		
		MOVLW		B'11010000'
		MOVWF		INTCON
		
		BSF		RCSTA, SPEN
		BSF		STATUS, RP0
		BCF		TXSTA, SYNC
		BSF		TXSTA, BRGH
		CLRF		SPBRG
		BSF		TXSTA, TXEN
		BCF		STATUS, RP0
		BSF		RCSTA, CREN
		
		CLRF		SLAVE_FREE
		
		CLRF		GUN_Y
		CLRF		GUN_X
		CLRF		GUN_FLAGS
		
		GOTO		START

SEND_COMMAND	; ARG0: address, ARG1: command
		BTFSS		SLAVE_FREE, 0
		GOTO		$ - 1
		BCF		INTCON, INTE
		BCF		PORTB, MASTER_BUSY
		MOVFW		ARG0
		MOVWF		TXREG
		MOVFW		ARG1
		MOVWF		TXREG
		BSF		STATUS, RP0
		BTFSS		TXSTA, TRMT
		GOTO		$ - 1
		BCF		STATUS, RP0
		BSF		PORTB, MASTER_BUSY
		BCF		INTCON, INTF
		BSF		INTCON, INTE
		BCF		SLAVE_FREE, 0
		RETURN
		
START
		CLRF		TEMP0
		CLRF		TEMP1
LOOP
		BTFSS		INTCON, T0IF
		GOTO		$ - 1
		BCF		INTCON, T0IF
		
		MOVLW		D'98'
		MOVWF		ARG0
		MOVFW		GUN_Y
		SUBLW		H'00'
		MOVWF		ARG1
		CALL		SEND_COMMAND
		
		MOVLW		D'99'
		MOVWF		ARG0
		MOVFW		GUN_X
		MOVWF		ARG1
		CALL		SEND_COMMAND
		
		GOTO		LOOP

NOP10CYCLES	NOP
		NOP
		NOP
		NOP
		NOP
		NOP
		RETURN
		
		END
